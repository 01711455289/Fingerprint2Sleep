apply plugin: 'com.android.application'
apply plugin: 'AndResGuard'

android {
    compileSdkVersion Integer.parseInt("$compileSdkVer")
    buildToolsVersion "$buildToolsVer"

    defaultConfig {
        applicationId "com.ztc1997.fingerprint2sleep"
        minSdkVersion 23
        targetSdkVersion Integer.parseInt("$targetSdkVer")
        versionCode 8
        versionName "0.6.1"
    }

    signingConfigs {
        release {
            def File signingConfFile = file("../signing.properties")
            def Properties signingConf = new Properties()
            signingConf.load(new FileInputStream(signingConfFile))

            storeFile file(signingConf['storeFile'])
            storePassword signingConf['storePassword']
            keyAlias signingConf['keyAlias']
            keyPassword signingConf['keyPassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            pseudoLocalesEnabled true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile project(':dpreference')

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // DSL
    compile "org.jetbrains.anko:anko-sdk23:$ankoVersion"

    // Firebase
    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"
    compile "com.google.firebase:firebase-ads:$firebaseVersion"

    compile 'com.eightbitlab:rxbus:1.0.2'

    compile 'com.orhanobut:logger:1.15'

    compile 'eu.chainfire:libsuperuser:1.0.0.201608240809'

    provided 'de.robv.android.xposed:api:82'
}

andResGuard {
    mappingFile = file("./resource_mapping_app-release.txt")
    // mappingFile = null
    use7zip = true
    useSign = true
    // 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
    keepRoot = false
    whiteList = [
            // for your icon
            "R.mipmap.ic_launcher",
            // for fabric
            "R.string.com.crashlytics.*",
            // for umeng update
            "R.string.umeng*",
            "R.string.UM*",
            "R.string.tb_*",
            "R.layout.umeng*",
            "R.layout.tb_*",
            "R.drawable.umeng*",
            "R.drawable.tb_*",
            "R.anim.umeng*",
            "R.color.umeng*",
            "R.color.tb_*",
            "R.style.*UM*",
            "R.style.umeng*",
            "R.id.umeng*",
            // umeng share for sina
            "R.drawable.sina*",
            // for google-services.json
            "R.string.google_app_id",
            "R.string.gcm_defaultSenderId",
            "R.string.default_web_client_id",
            "R.string.ga_trackingId",
            "R.string.firebase_database_url",
            "R.string.google_api_key",
            "R.string.google_crash_reporting_api_key",
            // umeng share for facebook
            "R.layout.*facebook*",
            "R.id.*facebook*",
            // umeng share for messager
            "R.layout.*messager*",
            "R.id.*messager*",
            // umeng share commond
            "R.id.progress_bar_parent",
            "R.id.webView"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
            "resources.arsc"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.1.13'
        //path = "/usr/local/bin/7za"
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
        classpath 'com.tencent.mm:AndResGuard-gradle-plugin:1.1.13'
    }
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
